label next_the_building:
    $ renpy.pause (0.8)
    stop music fadeout 3
    scene bg poldvor
    "У подъезда дома поджидал неприятный сюрприз. Тело замерло в решительном ступоре."
    play music raz fadein 7
    th "Полиция?! Как они узнали? Черт, черт, черт!"
    "Мнительность нашептывала тысячу причин; ей вторил адреналин: руки предательски затряслись, сердце бешено заколотилось в каком-то бесноватом ритме."
    "Вся утренняя вальяжность тут же испарилась."
    $ renpy.pause (0.3)
    "Я робко попытался вернуть контроль над телом."
    th "Так, так, так, сейчас надо просто успокоиться. Тремор и тяжелое дыхание - мои верные спутники уже много лет. Прямо-таки `Три товарища` Ремарка."
    th "Что же, мы с вами, дорогие, общего языка не найдем?"
    th "Полиция...Мало ли, по какому поводу она здесь. В этом районе живут сотни человек."
    "Аутотренинг, мягко говоря, вышел посредственным."
    th "Соберись, тряпка. Тебе нужно попасть домой, помнишь?"
    "Лишь сделав шаг, я осознал всю щекотливость собственного положения: ватные ноги извратили походку до неузнаваемости."
    th "Черт! {w}Не нужно привлекать лишнего внимания."
    "Предстоял самый важный участок: дорожка в непосредственной близости перед полицейской машиной. Страж порядка, сидя за рулем, не обращал, кажется, никакого внимания на происходящее вокруг."
    "Я постарался преодолеть путь с максимально отрешенным видом, готовясь, наконец-то, переступить порог подъезда и вдоволь разрыдаться."
    "И мне почти удалось реализовать задуманное, как вдруг..."
    pol "Молодой человек!"
    play sound ser
    th "МЕНЯ ОТКЛИКНУЛИ?!"
    $ renpy.pause (0.3)
    stop music fadeout 5
    pol "Сергеев Семен?"
    play music vibor fadein 2
    "Инстинкт требовал лишь одного."

    menu:
        "Пуститься в бега":
            stop sound
            play music pobeg fadein 2
            $ renegat += 2
            "Я мгновенно сорвался с места, устремившись куда-то вглубь дворов."
            pol "Стоять!"
            "Крик стража порядка лишь раззадоривал и добавлял скорости."
            show bg pob with dissolve
            "Ноги несли меня в неизвестном направлении." with vpunch
            pol "КОМУ ГОВОРЮ, СТОЯТЬ!" with hpunch
            "Истошный вопль полицейского отчетливо слышался за спиной." with vpunch
            th "Он догоняет!" with vpunch
            "Тело явно не было готово к подобным нагрузкам: легкие рапортовали о скором выходе из строя." with hpunch
            play sound ser
            th "Нельзя...Нельзя...Никак нельзя..." with vpunch
            "Редкие пешеходы с удивлением наблюдали разыгравшуюся сценку." with vpunch
            pol "ДЕРЖИТЕ ЕГО!" with vpunch
            th "Что же случится, если они меня поймают?!" with vpunch
            zs "Ничего хорошего!" with vpunch
            "Теперь о себе заявила боль в ступнях - старые кеды не были предназначены для беспощадного забега по снегу." with vpunch
            "Впереди виднелось разветвление дороги. Ответственный момент, определяющий исход погони и мою дальнейшую судьбу." with vpunch
            th "Куда бежать? Лишь бы только угадать!" with hpunch
            stop sound
            show bg typic with dissolve
            th "Нет, нет, нет!" with vpunch
            "Я был готов завыть от опустошающего чувства бессилия." with hpunch
            "Широко улыбаясь, меня встречал тупик." with vpunch
            th "Здание...Дверь..." with vpunch
            "Мысли старательно путались, а силы стремительно покидали." with vpunch
            play sound sfx_knock_door_closed_hard1
            "Закрыто!"
            $ renpy.pause (0.3)
            "Это конец. Я проиграл. Торжествуй, безликая система."
            stop music fadeout 5
            pol "Сер...Ух...Сергеев Семен?"
            play music raz fadein 2
            "Погоня не прошла даром и для догоняющего."
            "Я опустошенно кивнул."
            pol "Ты...Вы являетесь подозреваемым в совершении преступления, по статье 159 УК РФ...За которое может быть назначено наказание в виде...Лишения свободы..."
            th "Как же они смогли вычислить меня?"
            pol "Я уполно...мочен доставить вас в орган следствия."
            "Перед уже глазами вовсю мелькали кадры из детства."
            pol "Также...Уф..Ваши действия при задержании будут рассмотрены в соответствии со статьей 19.03 КоАП РФ."
            pol "Вы имеете право на..."
            $ discom = True
            show blink with dissolve
            jump judgement

        "Вступить в диалог с полицейским":
            stop sound fadeout 5
            stop music fadeout 5
            $ paragon += 1
            play ambience gorod fadein 2
            "Я медленно развернулся к вопрошающему."
            play music raz fadein 3
            me "Д-да?"
            "Страх мешал внятно изъясняться. Жалкое зрелище, должно быть."
            pol "Вы, Сергеев Семен Кондратьевич, являетесь фигурантом дела и главным подозреваемым в совершении преступления, в соответствии со статьей 159 УК РФ."
            "Он протянул мне несколько листков с печатями. Постановление о задержание, насколько я понял беглым взглядом."
            "Могло ли это быть досадной ошибкой?"
            me "Какая-то глупость..."
            "Ответил я дрожащим голосом, открывая новые горизонты трусости."
            pol "Установлена ваша предварительная причастность к противоправным действиям, именуемым мошенничеством."
            me "Н-но...Я ведь не совершал никаких таких...Действий."
            th "Так себе блеф."
            pol "С этим будут разбираться уже иные люди. Пока же, на данной стадии расследования, факты говорят об обратном."
            "Обида сковывала, подогревая злость на самого себя. Почему, как, в чем я умудрился просчитаться?"
            "Полицейский неожиданно улыбнулся, чем заметно меня озадачил."
            pol "Не так уж все и плохо, Семен Кондратьевич. Я же тут почти с самого утра вас дожидаюсь, а не появились бы - так в розыск отправились, жизнь себе усложнив порядком."
            th "А сейчас, видимо, ощущаю легкость бытия."
            pol "Хорошо хоть...{i}Неравнодушное население{/i} в лице вашей соседки многое прояснило."
            th "И ты, Брут..."
            pol "И все же, я уполномочен доставить вас в орган следствия. Вы имеете право на..."
            show blink with dissolve
            jump judgement

