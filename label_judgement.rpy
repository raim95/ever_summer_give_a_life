label judgement:
    stop music
    stop ambience
    stop sound
    play music bbad fadein 2
    scene bg black with dissolve
    $ set_mode_nvl()
    "Рано или поздно, но это должно было произойти. Конечно, я всегда искренне, как и подобает всякому недалекому преступнику, надеялся на счастливый билет: старательно обеспечивая анонимность за счет доступных средств и здравого смысла, никак не хотелось отправляться за решетку."
    "В моей жизни не было места Сони Мармеладовой, потому и жалел лишь об одном: о том, что, все же, умудрился попасться."
    "Знакомая многим история: мне банально не повезло. Возможно, удавалось бы еще долго продавать убогие копии под видом оригинальных смартфонов в соц. сетях, если бы не его величество случай."
    "Если бы, однажды, покупателем не стала юная племянница городского прокурора, например. Или, если бы, тот, представим, относился к ней сдержанно и прохладно, а не исполнял всякий каприз наивной школьницы."
    "Так или иначе, не будь я неудачником, ничего подобного бы не случилось. Наверное."
    "Средства позволяли нанять толкового адвоката, но он откровенно заявил, что сухим из воды выйти не получится. Будет устроена показательная порка; мне же в ней заботливо уготована главная роль."
    "Следствие, при задействованных административных ресурсах, велось в стремительном режиме."
    "Вместе с адвокатом мы основательно прорабатывали линию защиты, вплоть до дотошной репетиции слов на судебном заседании. Я, в принципе, прекрасно осознавал перспективы: в лучшем случае, ждут исправительные работы, в худшем - реальный срок в тюрьме."
    "\nНаконец, настало время понести ответственность за содеянное, а еще лучше - избежать. Моя роль была проста: нужно лишь придерживаться образа раскаявшегося человека, надеясь на взвешенное решение судьи."
    $ set_mode_adv()
    $ renpy.pause (0.4)
    stop music fadeout 5
    scene bg syd with dissolve
    "Процессия находилась в самом разгаре. Адвокат с ловкостью парировал бесконечные выпады обвинения."
    play music syd1 fadein 4
    play ambience sydsh
    th "Он действительно может склонить чащу весов в нужную сторону."
    "Волнение, впрочем, продолжило новое наступление."
    "Ощущаю острый приступ духоты - расстегиваю верхнюю пуговицу рубашки, при этом тяжко сглатываю вязкую слюну."
    "Во время последнего выступления, прокурор, женщина довольно преклонных лет и необъятных габаритов, опрокидывает меня презрительным взглядом. Ее цель была очевидна и ясна."
    "Сам же я готов был умереть от стыда. Очередное сокрушительное фиаско в мою честь."
    th "А ведь бабушка любила повторять: доведет безделье до беды. Ну, в данном случае, довела заместитель - тяга."
    $ renpy.pause (0.4)
    "Все присутствующие, вдруг, резко перевели взгляд в нашу сторону. Явственней знака не придумать."
    "И вот, судья обращается ко мне."
    th "Придерживаться легенды..."
    stop music fadeout 5
    syd "Подсудимый, встаньте. Сергеев Семен Кондратьевич, признаете ли вы свою вину?"
    play music syd2 fadein 2

    menu:
       "Безусловно, признаю":
            stop music fadeout 5
            me "Уважаемый судья и все здесь присутствующие."
            play music syd1 fadein 2
            th "Так, неплохое начало. Что дальше?"
            me "Я абсолютно искренне {i}раскаиваюсь в содеянном и признаю свою вину во всех эпизодах, доказанных следствием."
            me "Мне, говорю честно и открыто, невероятно стыдно и мерзко. От себя и от всего того тошного варева, что я затеял."
            "Массовка внимала каждому слову. Нельзя сбавлять темп!"
            me "Конечно, не от хорошей жизни пришлось ввязаться в эту авантюру. Я был недальновиден, глуп и эгоистичен, занимаясь подобным безрассудством."
            th "Прямо так и без суфлера? Не верю."
            me "Уважаемый суд, я целиком и полностью признаю свою вину и прошу лишь толику вашего снисхождения, если оно имеет место быть."
            me "Мне бы хотелось исправиться, проявив свои гражданские качества и принеся пользу обществу...На этом все, спасибо за уделенное внимание."
            "После такого обычно уединяются с сигаретой."
            syd "Благодарю, подсудимый. Суд удаляется для вынесения вердикта."
            "Опускаю взгляд и замечаю одобрительный кивок моего союзника. Кажется, нам таки удастся оседлать фемиду."
            show blink with dissolve
            scene bg black with dissolve
            $ renpy.pause (0.4)

       "Я невиновен":
            stop music fadeout 5
            $ renegat += 1
            me "Уважаемый судья и все здесь присутствующие."
            play music syd1 fadein 2
            th "Так, неплохое начало. Что дальше?"
            me "Происходящее здесь - досадная или намеренная ошибка следствия, что упорно пытается оклеветать невиновного человека."
            th "Дерзко. Сработает ли?"
            me "К разбираемому здесь делу я не имею абсолютно никакого отношения. Моя вина заключается лишь в том, что я удачно подвернулся для раскрытия, так называемого, `висяка`."
            "Прокурор бросила ненавистный взгляд."
            me "Так вот...Не думаю, что стоит занимать ваше драгоценное время и повторять слова защитника; слова, между прочим, весьма убедительные."
            me "Адвокат привел достаточно разумных доводов, потому остается надеяться, что вы, уважаемый суд, не позволите следствию себя запутать."
            th "Это не пройдет незамеченным. Слишком уж наглая ложь."
            syd "Благодарю, подсудимый. Суд удаляется для вынесения вердикта."
            "Медленно опускаю взгляд и замечаю обеспокоенную физиономию моего союзника. Видимо, стоило придерживаться оговоренного плана."
            show blink with dissolve
            scene bg black with dissolve
            $ renpy.pause (0.4)

    stop music
    scene bg syd with dissolve
    "Спустя двадцать минут, судья все же вернулся. Ожидание приговора было невыносимым."
    play music depra fadein 4
    syd "Суд, изучив материалы дела, а также доводы сторон пришел к следующему заключению..."
    th "Не томи, ну же!"

    if renegat == 4:
        syd "Сергеева Семена Кондратьевича признать виновным в  соответствии  со статьей 159 УК РФ, и назначить ему меру наказания в виде лишения свободы на протяжении 18-ти месяцев в колонии общего режима."
        th "Это невозможно...Не верю..."
        "Принять досадное поражение я никак не мог."
        syd "Приговор может быть обжалован в апелляционном порядке в течение 10 суток со дня провозглашения. Подсудимого, Сергеева Семена Кондратьевича, взять под стражу непосредственно в зале суда."
        "Я был раздавлен. Какое будущее ожидает за решеткой? Я заплатил слишком высокую цену за глупую авантюру."
        me "Нет, это какая-то ошибка, послушайте! Почему так, почему тюрьма..."
        "Истерика - старый спутник стадии отрицания."
        me "Послушайте, пожалуйста, все это неправильно...Да, оступился, но оступился лишь раз, неужели заслуживаю подобного?!"
        "Я ощутил соленый вкус слез на губах."
        "Судья оставался непреклонен: он слишком часто становился свидетелем подобных сцен, чтобы хоть как-то на них реагировать."
        th "Необходимо обжаловать приговор!"
        stop ambience
        stop music fadeout 5
        th "Хотя бы, попытаться..."
        scene bg black with dissolve
        $ renpy.pause(4)
        play sound sfx_achievement
        window show
        "Плохая концовка: кто тут смотрящий?"
        window hide
        $ renpy.pause(2.0)
        return

    else:
        syd "Сергеева Семена Кондратьевича признать виновным в соответствии со статьей 159 УК РФ, и назначить ему меру наказания в виде исправительных работ на протяжении 12-ти месяцев."
        "Я вздохнул с облегчением: все-таки удалось выйти с минимальными потерями из непростой ситуации."
        syd "Суд посчитал, что исправление может быть достигнуто без изоляции от общества и удаления из привычной среды обитания, лишь средствами трудового воздействия, работой в коллективе."
        th "Не самый худший исход."
        syd "Приговор может быть обжалован в апелляционном порядке в течение 10 суток со дня провозглашения. Конкретный вид работ будет определен органами местного самоуправления по месту жительства осужденного."
        th "Надеюсь, они не заставят подметать дворы. Между прочим, это довольно унизительно."
        syd "Заседание суда объявляется закрытым!"
        stop music fadeout 2
        scene bg black with dissolve
        $ renpy.pause(2.0)
        stop ambience
        jump at_work
