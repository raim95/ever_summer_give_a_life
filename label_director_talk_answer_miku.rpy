label director_talk_answer_miku:
    scene bg olk with dissolve
    stop music fadeout 4
    "Благодаря множественным подсказкам Виолы, путь до гнезда заведующей занял совсем уж незначительное время."
    play music kvar fadein 6
    show mt angry with dissolve
    mt "Семен! Тебя только за смертью посылать! Почему так долго?"
    me "Был вынужден отвлечься по пути."
    mt "Отвлечься?! Нет, ну, какая вопиющая безответственность! Я ведь просила закончить побыстрей!"
    "Негодование Ольги стало во истину чрезмерным."
    th "Пора поделиться чувством стыда."
    me "Да, к сожалению. Ко мне обратилась пациентка за помощью; проигнорировать такое, сами понимаете, - преступление против человечества. Отказать не было никакой возможности."
    show mt norm
    "Боевая удаль мгновенно сошла с лица начальницы, на смену ей ступила растерянность."
    mt "Ах, ну…Конечно, если так, то…"
    mt "Молодец."
    "Пора окончательно перехватывать инициативу на себя."
    me "Ольга Дмитриевна, чем теперь необходимо заняться?"
    mt "Ну, раз ты уже начал знакомство с  постояльцами, то, думаю, настала пора становиться куратором."
    th "Второй день, а такой-то карьерный рост. Впечатляет!"
    zs "Конечно. Наглость – универсальная отмычка."
    mt "Как правило, за каждым сотрудником закреплено несколько человек, но конкретно в этом случае ограничимся пока одним."
    me "Хорошо. Что нужно делать?"
    mt "Следить за состоянием здоровья, всячески опекать и обеспечивать приятный досуг."
    me "Сиделка, в общем."
    mt "Давай-ка без вульгарностей!"
    "Ольга о чем-то ненадолго задумалась, прежде чем продолжила."
    mt "А начнем, пожалуй, с Лидии Михайловной. Той самой, которая тебе родней по версии Славяны приходится."
    mt "Приятная пожилая женщина, внимательно следит за собой…Только, Семен, пожалуйста, будь максимально ответственен."
    me "В плане?"
    mt "Малейшая жалоба, недомогание или что-то подобное – сразу сообщай сотрудникам. Никакой самодеятельности."
    th "«Неужели все так плохо…»"
    mt "Вопросы?"
    me "Нет. Глупые вы не переносите, а другие – не мой профиль."
    show mt smile
    "Заведующая тепло улыбнулась."
    zs "70 килограммов отборного лицемерия. Не стыдно?"
    th "Оно никому не вредит."
    stop music fadeout 4
    $ renpy.pause(1.0)
    scene bg koridor with dissolve
    "Не хотелось хвастаться, но общение с пожилыми людьми всегда сдавалось на отлично."
    play music tpss fadein 7
    "С теплотой в сердце и одновременно, каким-то детским трепетом, вспоминаются добрые вечера у бабушки: она зазывала подруг, а те, в перерывах между бесконечным чаепитием, безустали осыпали меня комплиментами."
    "То был пик женского внимания за недолгие двадцать, в общем-то. Чуткость, скромность и умение слушать  - универсальная связка ключей к тем, кому за пятьдесят."
    "Картину омрачала лишь специфика работы: честно говоря, я не вполне представлял, как нужно взаимодействовать с постояльцами."
    "Вот они что-то увлеченно рассказывают, шутят, смеются и все это происходит в нарастающей тени гнусной болезни. Привкус безнадежности не смогут перебить даже 5 ложек сахара, уверен."
    "И все же, они продолжали жить, как делали это ранее."
    "Ольга заявляла о чудесных случаях исцеления, но есть ли вера ее словам? Идеализм мешает трезво оценивать происходящее."
    zs "Как и пессимизм, впрочем."
    th "Нет, он, как минимум, честен."
    zs "А еще любит настойчиво приглашать старую подругу погостить."
    th "Ты о депрессии?"
    zs "Именно."
    "Я и не заметил, как добрался до нужного кабинета, крепко задумавшись. Уведомив предварительным стуком о появлении гостя, я распахнул дверь."
    scene bg hosko with dissolve
    "В комнате расположились две пожилых особы, о чем-то увлеченно беседующие на громких тонах."
    show lm norm with dissolve
    th "Есть силы спорить – уже хорошо."
    "Правая дама быстро привлекла внимание своим видом: гармонично подобранная одежда, аксессуары к месту, элегантные очки и довольно пышная шевелюра, что вызывало заметный диссонанс."
    zs "Парик, приятель. Она все еще пытается ухватиться за прежнюю жизнь. Или, скорее даже, просто - за жизнь."
    "Спор иссяк. Пациентки с неподдельным интересом рассматривали диковинного зверя."
    zs "Эй, сыч – это птица, а не зверь."
    th "Спасибо, гений."
    "К несчастью, строгость также прослеживалась в ее внешнем образе: не очень-то хотелось попасться во власть авторитарной старушки."
    th "По всей видимости, это и есть моя подопечная."
    "Если честно, то подобная картина порядком вводила в ступор. Я ожидал встретить, скорее, «овощеподобных» больных, нежели относительно нормальных."
    me "Доброго вам дня, и, заодно, извините, что отвлекаю. Зовут меня Сергеев Семен, по совместительству – новый сотрудник хосписа."
    "Женщинам знакомство пришлось по душе, что стало ясно по одобряющим выражениям лиц."
    me "Ольга Дмитриевна назначила на курирующую должность к вам, Лидия Михайловна. Но…"
    "Я театрально растерялся, демонстративно разведя руками в стороны."
    me "Поскольку, к сожалению, совсем начинающий, то не вполне представляю список своих обязанностей…Попрошу отнестись с пониманием."
    "А сейчас подключаем робкую улыбку."
    lm "Да вы присаживайтесь, молодой человек, что в дверях стоять."
    "Женщина указала на солидных размеров стул."
    lm "Вот, Зоя, а ты молодежь пинаешь до устали: гляди, сколько помощников юных!"
    show lm norm2
    "Компаньон нехотя согласилась."
    me "Как вы себя чувствуете?"
    "«Все в порядке!»"
    "Ответили синхронные голоса."
    "Вновь пауза."
    th "Так, что там в списке универсальных тем…Погода?"
    lm "Ну, Семен, расскажите о себе, что молчите. Работничков здесь немного, каждое свежее лицо – в радость."
    th "Чтобы рассказать… Автобиографию? Уместится в одном тоскливом абзаце."
    menu:
        "Поделиться забавной историей, прочитанной в сети":
            "..."
            $ renpy.pause(1.5)
            $ renegat += 1
            lm "Неужто и в самом деле такое приключается с кем-то?"
            "Старая паста, к счастью, нашла непосредственный отклик и у пожилых слушателей."
            me "Живой свидетель перед вами, тем не менее."
            lm "Ох, забава, Семен, в самом деле забава!"
            show lm norm3
            "Дамам история действительно пришлась по душе, по крайней мере, румянец на щеках и непрекращающиеся смешки свидетельствовали именно об этом."
            zs "Занятно, да? В твоей-то жизни нет подобных уморительных событий."
            zs "Скажу иначе, события в ней по большим праздникам и лишь в високосные года."
            th "Это не так. Арест, суд, первая работа. Впечатлений предостаточно."
            zs "Не глупи. Все перечисленное - лишь секундная вспышка на одинаковом пути с однотипными декорациями. Что было ранее? Заунывная тоска и бытовуха – вся жизнь в формате овоща."
            zs "К этому ты всегда стремился? Стать заурядной массовкой?"
            "Злободневные вопросы, что стали риторическими. Пригласительный билет в край никчемного существования. Мечтал о финансовой независимости? Так кушать поданно - строгий график и 15000 рублей в месяц."
            zs "Радуйся, что не посменно – хоспис-то круглосуточный."
            lm "Семен, ты чего, задумался что ли?"
            th "Со стороны и впрямь выглядел как аутист."
            me "Простите…"
            lm "А давайте-ка чаю!"
            "Незаметно, за разговорами и интенсивными расспросами насчет всего на свете, прокрался вечер."
            scene bg black with dissolve

        "Рассказать о детстве":
            $ paragon += 1
            "Я принялся делиться сокровенными воспоминаниями. Аудитория оказалась на редкость благодарной, полностью увлеченная моими историями."
            "..."
            $ renpy.pause(1.5)
            lm "Семен, бабушка твоя – добрейший человек, это ясно как божий день. Уж с какой теплотой ты об этом говоришь, тут никаких сомнений. Сколько ей сейчас?"
            me "К сожалению, ее больше нет. Полтора года, как ни стало."
            th "Черт, зря затеял эту тему! С такой-то концовкой, идиот!"
            "Я порядком смутился, женщины, впрочем, не отставали."
            show lm norm3
            lm "Семен, мои соболезнования. Главное, что человек прожил достойную жизнь – с такой мыслью и уйти ведь нестрашно, верно?"
            "Удивительно. Сейчас она рассуждает о жизненных ценностях, слушает монологи малознакомого парня, комментирует их; позже обсудит с подругой за теплой кружкой зеленого и…"
            "И одна из таких банальнейших сценок, в скором времени, окажется последней. Как же удается сохранять спокойствие, зная о том, что отведенный срок вот-вот закончится?"
            "Я испытывал тупую, давящую жалость попеременно с нечтом, как ни странно, наподобие зависти: необходимо обладать решительной смелостью, чтобы смотреть смерти в глаза, приговаривая: «Еще не время, старуха. Не сейчас.»"
            zs "У этих женщин мужественности поболее твоей."
            lm "А давайте-ка чаю!"
            "Незаметно, за разговорами и интенсивными расспросами насчет всего на свете, прокрался вечер."
            scene bg black with dissolve

    $ renpy.pause(1.0)
    scene bg hosko with dissolve
    show lm norm with dissolve
    "О завершении рабочего дня уведомил приход Ольги."
    show mt norm at right with dissolve
    mt "Доброго вечера! Не против, я Семена у вас заберу?"
    lm "Не хотим, но куда деваться – забирайте!"
    "Мне, вполне ожидаемо, удалось предстать перед постоялицами в самом выгодном свете. Прощались они с заметной неохотой."
    stop music fadeout 4
    scene bg koridor with dissolve
    show mt angry at right with dissolve
    "Заведующая вывела меня в коридор. Читалось в ее взгляде что-то нехорошее."
    play music depra fadein 3
    mt "Семен…"
    "От одного только обращения несло презрением за версту."
    mt "Я не терплю лжецов. А ты. Лжец. Патологический."
    th "О чем она говорит?"
    mt "Мику все рассказала: и о том, как ты нагло с ней заигрывал, наплевав на мой указ, и о том, как отзывался о служебных обязанностях."
    mt "Более того, ты пытался ввести в заблуждение еще меня и Виолу. Нет, ну, в тебе есть хоть капля человеческого?!"
    th "Мику это та зеленоволосая? Вот же сучка!"
    me "Послушайте, Ольга Дмитриевна, это все не…"
    show mt rage at right
    mt "ДА НЕ ЖЕЛАЮ Я ТЕБЯ СЛУШАТЬ БОЛЬШЕ!"
    show mt angry at right
    "Все. Никакого реванша не будет. Безоговорочная капитуляция."
    "Зеваки на фоне обратили на нас самое пристальное внимание, по всей видимости, предвкушая увлекательное зрелище."
    th "Чего молчишь? Где желчь, где яд?"
    th "Ясно."
    show mt rage at right
    mt "Я СЕГОДНЯ ЖЕ СОСТАВЛЮ ОБРАЩЕНИЕ В ОРГАНЫ НАДЗОРА, И ПУСТЬ ОНИ РЕШАЮТ, ЧТО С ТОБОЙ ДЕЛАТЬ!"
    show mt angry at right
    "Это уже походило не на крик, а на вполне солидную истерику."
    show mt norm at right
    mt "Виола была права насчет тебя. Зря я в это только ввязалась…"
    th "Виола-то тут причем?"
    "Терять нечего."
    zs "Чувствуешь, старина? Так в жилах закипает ненависть. Тебя подставили, причем шутки ради."
    "Обычно, в подобных ситуациях, мне не хватает смелости дать хоть какой-то отпор, но сейчас я искал силу у зарождающейся злости."
    me "Какого черта вы обрушиваете на меня беспочвенные обвинения?"
    "Начальница порядком опешила от вызова."
    me "Эта, как вы сказали, Мику – бесноватая девица специально отвлекала от выполнения задания, доставая с бессмысленными расспросами. Ей удалось украсть пару минут, которых, видимо, оказалось достаточно."
    me "В ситуацией с Виолой: банально растерялся, ляпнул глупость, согласен – поступил недальновидно. Но если поразмыслить объективно, то просто стал жертвой обстоятельств."
    me "Разве стал бы я столь глупо подставляться, учитывая мое положение?"
    "Ольга слишком долго молчала, переваривая, полностью утратив любую инициативу."
    mt "Высказался? Мое решение это не изменит. Хоспис нуждается в других людях…"
    show sl norm at left with dissolve
    sl "Ольга Дмитриевна…"
    "В разговор неожиданно вмешалась Славя."
    th "Фортуна, ну, что с тобой не так?"
    sl "Я случайно оказалась неподалеку, услышала ваши повышенные тона…Так получилось, что суть разговора уловила."
    show mt angry at right
    "До чего чудесная ситуация. Хотел произвести впечатление на девушку – предстал чудовищем."
    show sl sad at left
    sl "Не стану оправдывать Семена, поскольку не знаю всей правды, но насчет Мику – вы же знаете ее странности. Вспомните хотя бы недавнюю историю с Шуриком!"
    "Ольга сверлила Славю самым пронзительным взглядом. Ненависть на лице заведующей быстро сменилась усталостью. Наконец, она повернулась ко мне."
    stop music fadeout 7
    show mt norm at right
    show sl norm
    mt "Жду завтра утром подробную объяснительную в своем кабинете. И ни на что особо не рассчитывай."
    "После чего мгновенно удалилась."
    play music bgood fadein 3
    hide mt with dissolve
    "Я виновато взглянул на Славяну. С какой целью девушка вызвалась помочь, приняв сторону гонимого отброса? Из-за жалости?"
    zs "Нет никаких сомнений. Ничего иного вызвать не способен."
    me "Спасибо, Славя. Но, наверное, не стоило вмешиваться в безнадежное дело – теперь пострадает репутация первого альтруиста хосписа."
    sl "Не нужно, Семен. Я, почему-то, верю твоим словам, плюс, зная Мику…А насчет Ольги Дмитриевной не переживай. Человек импульсивный, но отходчивый."
    show sl smile
    "Блондинка одарила фирменной улыбкой."
    th "Полегче, красавица, из-за такого голову теряют быстрее, чем на плахе."
    me "Ты очень добра ко мне, чего совершенно не заслуживаю. Чашечка латте в качестве искренней благодарности подойдет?"
    zs "Заметь, даже не смутилась. С такой-то внешностью сотни однотипных предложений в день."
    show sl smile1
    sl "Милое приглашение, в самом деле, но скоро за мной приедет папа, извини."
    th "Отлично. Пойду сдохну в одиночестве, до скорых встреч!"
    show sl norm
    sl "Знаешь, Семен, а вообще…Если хочешь, то можешь проводить до автобусной остановки. Туда он и подъедет."
    th "Цыц, гордость. Должен, как минимум, во имя благодарности."
    me "Конечно, Славя. Мне тебя подождать в гардеробной?"
    stop ambience fadeout 4
    stop music fadeout 4
    sl "Да, буквально минутку!"
    $ renpy.pause(1.5)
    scene bg hoshol with dissolve
    play music vech fadein 3
    show sl norm with dissolve
    "Размышляя над событиями дня, я и не заметил появления девушки."
    sl "Семен, идем?"
    me "Слушай, а давай здесь подождем?"
    show sl syrp
    "Недоумение посетило лицо собеседницы."
    me "Там довольно холодно… Уверен, что твой папа сообщит о своем приезде. Тогда и выйдем, ок?"
    show sl norm
    sl "Ну, ладно, давай здесь…"
    menu:
        "Выразить благодарность":
            $ slav_res += 1
            show sl norm
            me "В самом деле, я тебе очень признателен, Славя."
            sl "Ой, Семен, договорились же, что хватит благода…"
            me "Нет-нет, послушай! Если бы не это вмешательство, то мне бы пришлось мириться с большими сложностями. Эта работа слишком важна, чтобы просто ее потерять."
            show sl sad
            sl "Это правда?"
            "Девушка говорила совсем тихо."
            show sl norm
            sl "Насчет того, что хоспис – мера наказания?"
            "Скрывать нечего. Славяна пришла на выручку, даже осознавая обилие грязи, преследующую меня."
            me "Да."
            show sl evil
            "Вгляд девушки стал невыносимо серьезен."
            "Да, именно так, она смотрела серьезно, взгляд был не пустой, нет, но ему не хватало привычной доброжелательности, оттого ее лицо и стало таким отторгающим."
            me "Я не хотел этим заниматься, но так получилось, к сожалению."
            zs "Обычно так оправдываются в классе, этак, пятом."
            me "Будь малейший шанс, намек – хоть что-то, способное исправить ошибки прошлого. А так…Неподъемный багаж проблем. Вот и мучаюсь."
            "Славяна заглянула мне в глаза. Не выдержал, струсил."
            show sl norm
            sl "Семен, но ведь в произошедшем виноват лишь ты и только. Люди любят ссылаться на других, на злой рок и тому подобное, отказываясь нести ответственность за собственные поступки."
            zs "Юная девица учит жизни. Записывай."
            me "Понимаю."
            "Комната перешла в распоряжение тягостной тишины. Секунды тянулись предательски долго."
            show sl sad
            sl "Зачем все это? Легкие деньги?"
            me "Я всегда считал себя особенным. По-детски презирал общество, находил окружающих тупыми и поверхностными. Честных трудяг клеймил рабами. Не позерства ради, нет; к ужасу, я совершенно искренне придерживался таких установок."
            me "Потом это дельце…Быстрые деньги, что заработаны на глупости людей. Конечно, это лишь убеждало в собственной правоте."
            show sl norm
            me "Но вот арест, суд, приговор. И отчетливое понимание того, что я себя представляю. Ни-че-го. Сплошная никчемность."
            "Моя речь, очевидно, тронула впечатлительную девушку. Сам же, признаюсь, не был уверен в абсолютной своей искренности."
            "Славя молча слушала, внимая каждому слову."
            me "Ты спрашиваешь зачем это? Да незачем. Все пустое: и авантюра глупая, и будни, и жизнь, и сам под стать."
            "Красавица с укором покачала головой."
            sl "Всегда неприятно слушать, когда кто-то о себе так отзывается. Да, пусть, ты поступал неправильно, ошибался, делал хуже! Главное, что за этим всем стоит человек, раскаявшийся и желающий исправиться!"
            show sl smile
            sl "Ты ведь хочешь?"
            menu:
                "Согласиться":
                    $ paragon += 1
                    me "Да. Очень хочу."
                    show sl smile1
                    sl "В мире так много неприятных вещей. Множество нехороших людей. Но это не повод замыкаться в себе и становиться озлобленным."
                    show sl norm
                    me "Разве я, не знаю, озлобленный? Ты правда так считаешь?"
                    sl "Я считаю, что ты можешь стать немного добрей."
                    me "Но у меня никогда не было достойного учителя."
                    show sl smile
                    sl "Тогда будем учиться вместе."
                    "Душевный диалог нарушил звук уведомления."
                    show sl norm
                    sl "А вот и папа…"
                    me "Идем."
                    scene bg ost with dissolve
                    "Весь непродолжительный путь до остановки мы молчали. Славя, по всей видимости, не знала о чем вообще со мной можно говорить; я же никак не мог подобрать достойные слова."
                    show sl kyr with dissolve
                    sl "Не грусти, Семен."
                    "Девушка робко обняла меня, чем заметно смутила."
                    "Тепло девичьего тела ощущалось как-то совершенно по-особенному, пробуждая давно забытые воспоминания."
                    "Я поддался и позволил себе секундную слабость, жадно вдохнув аромат волос Слави. То был легкий фруктовый оттенок: столь, одновременно, близкий, но, в той же степени, бесконечно от меня далекий."
                    sl "До завтра!"
                    hide sl with dissolve
                    "Сказав это, Славяна ловко скрылась в стоящей рядом машине."
                    "Мог ли я стать ей ближе? Ближе ровно настолько, насколько того требовала пыльная душонка, истосковавшаяся по людскому теплу."
                    "Стать частью жизни Слави: познакомиться с ее друзьями, приятелями, знакомыми, досугом и увлечениями; произвести положительное впечатление на ее родных, сидя за семейным столом."
                    "Для того необходимо избавиться от толстого «панциря», ставшего панацеей и крестным отцом зоны комфорта. Но стоит ли размен того?..."
                    "Меня ожидало тоскливое путешествие до дома."

                "Она не понимает, о чем говорит":
                    $ renegat += 1
                    me "Это все слишком сложно…Дело ведь не только лишь во мне."
                    show sl norm
                    sl "А в чем еще?"
                    me "Мы ведь не рождаемся кем-либо, а становимся таковыми в процессе становления."
                    me "И когда тебя окружают мерзавцы, а жизнь катиться под откос, то, знаешь, непросто оставаться альтруистом. "
                    show sl smile
                    sl "Я прекрасно понимаю то, о чем ты говоришь."
                    th "Конечно. Тебе-то, всеобщей любимице-активистке, только и знать о моих проблемах."
                    "Я равнодушно пожал плечами."
                    show sl norm
                    sl "Как-то это жестоко...Но неужели совсем нет ничего светлого в жизни?"
                    me "Пиво считается?"
                    show sl smile
                    "Диалог нарушил звук уведомления."
                    sl "А вот и папа…"
                    me "Идем."
                    scene bg ost with dissolve
                    "Весь непродолжительный путь до остановки мы молчали. Славя, по всей видимости, не знала о чем вообще со мной можно говорить; я же, уязвленный, не желал беседовать вовсе."
                    show sl kyr with dissolve
                    sl "Не грусти, Семен."
                    "Девушка робко обняла меня, чем заметно смутила."
                    "Тепло девичьего тела ощущалось как-то совершенно по-особенному, пробуждая давно забытые воспоминания."
                    "Я поддался и позволил себе секундную слабость, жадно вдохнув аромат волос Слави. То был легкий фруктовый оттенок: столь, одновременно, близкий, но, в той же степени, бесконечно от меня далекий."
                    sl "До завтра!"
                    hide sl with dissolve
                    "Сказав это, Славяна ловко скрылась в стоящей рядом машине. Меня же ожидало тоскливое путешествие до дома."

        "Попросить рассказать о Мику":
            $ miku_res += 1
            show sl norm
            "Многое в ее поведении казалось слишком уж странным и нелогичным. Может, я как-то умудрился обидеть ее?"
            me "А эта Мику, что с ней не так?"
            "Славя тяжко вздохнула."
            show sl sad
            sl "Представь, что ты родился в богатой семье. Твой отец – самовлюбленный бизнесмен с амбициями, а мама покоряет лучшие подиумы. Привык с детства к роскоши и трепетному отношению со стороны каждого постороннего."
            sl "Частный сад, затем элитная гимназия. Тебя взращивали, постоянно напоминая о превосходстве над остальными. А остальные – это что? Массовка, лицезрящая твои свершения. Безнадежные аутсайдеры."
            sl "А потом, однажды, отец приходит в твою комнату и говорит: «Знаешь, ты вырос чудовищем.»"
            sl "И подобно принцессе из сказки, тебя запирают в башне на замок. Только все несколько прозаичнее - его заменяет хоспис."
            me "Эмпатию нельзя навязать насильно."
            show sl norm
            sl "Я знаю, Семен, знаю. Но дети – продолжение родителей."
            th "Вот уж не согласен!"
            sl "И они точно также неспособны признать свою вину. Да даже допустить одну мысль о том, что где-то недосмотрели, где-то проявили безразличие."
            me "Наглая, избалованная девчонка, ясно. Отрыжка неправильного воспитания."
            sl "Не говори так. По-моему, за всей этой вызывающей ширмой, прячется одинокий и никем непонятый человек."
            me "А если она в самом деле отвратительна?"
            sl "Я вижусь с ней довольно часто, мы учимся в одном институте. И порой замечаю, что Мику хочет…Впрочем, неважно."
            show sl smile
            sl "И так наговорила лишнего."
            "Беседу нарушил звук уведомления."
            sl "Это папа…"
            me "Идем."
            scene bg ost with dissolve
            "Весь непродолжительный путь до остановки мы молчали. Славя, по всей видимости, не знала о чем вообще со мной можно говорить; я же, подавленный, не желал беседовать вовсе."
            show sl kyr with dissolve
            sl "Не грусти. Ольга Дмитриевна завтра сменит гнев на милость."
            "Я поддержал сказанное робким кивком."
            me "Хотелось бы верить..."
            sl "Так верь! До завтра!"
            hide sl with dissolve
            "Сказав это, Славяна ловко скрылась в стоящей рядом машине."
            "Теперь в Мику виделась пародия на самого себя с горьким привкусом: более самоуверенная, более озлобленная."
            "Вполне возможно, что колючая оболочка скрывает ранимого человека, пережившего множество вызовов судьбы."
            "Потенциал для развития взаимоотношений определенно имелся, но другой вопрос, - смогу ли я им воспользоваться."
            "Меня же ожидало тоскливое путешествие до дома."

    jump second_day